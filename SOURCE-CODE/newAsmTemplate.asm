    LIST P=18F4520
#INCLUDE <P18F4520.INC>
    CONFIG OSC=HS   ;CAU HINH BO TAO DAO DONG, SU DUNG THACH ANH 20MHz
    CONFIG MCLRE = OFF	;TAT CHUC NANG RESET NGOAI
    
    CBLOCK 0X00	;KHOI LUU TRU CAC BIEN BAT DAU TAI DIA CHI 0X00
CHUC	;BIEN DEM HANG CHUC
DONVI	;BIEN DEM HANG CHUC
DEM1	;BO NHO TAM CHO BIEN "CHUC"
DEM2	;BO NHO TAM CHO BIEN "DONVI"
DEMLAN1	;GIA TRI DEM TAO DELAY RELAY
ENABLE	;BIEN DIEU KIEN STOP/RESUME
ENABLE1	;BIEN DIEU KIEN STOP/RESUME
START	;BIEN DIEU KIEN ON/OFF
W_TEMP	;BIEN TAM THANH GHI W
STATUS_TEMP ;BIEN TAM THANH GHI STATUS
    ENDC

    ORG 0X00	;DIEM BAT DAU CHUONG TRINH CHINH
    GOTO MAIN
    ORG 0X08	;DIEM BAT DAU CHUONG TRINH NGAT
    GOTO NGAT
;;;;;;;;;;;;CHUONG TRINH CHINH;;;;;;;;;;;;;;;;
MAIN
    CLRF TRISD	;CAU HINH PORTA,C,D LA OUTPUT
    CLRF TRISC
    CLRF TRISA
    MOVLW 0X07
    MOVWF TRISB; CAU HINH RB<0:2> LA INPUT
    MOVLW 0X0F
    MOVWF ADCON1    ;CAU HINH AN<12:0> LA DIGITAL I/O
    MOVLW 0X07
    MOVWF CMCON	 ;CAU HINH COMPARATORS LA DIGITAL INPUT
    BSF INTCON,7    ;CHO PHEP DUNG NGAT
    BSF INTCON,4    ;CHO PHEP DUNG NGAT NGOAI INT0
    BCF INTCON2,7   ;SU DUNG KIEU NOI PULL-UP CHO RB<0:2>
    BCF INTCON2,6   ;CAU HINH NGAT CHAN RB0 O SUON XUONG
    BSF INTCON3,3   ;CHO PHEP DUNG NGAT NGOAI INT1
    BSF INTCON3,4   ;CHO PHEP DUNG NGAT NGOAI INT2
    BSF T0CON,7	;CHO PHEP TIMER0 HOAT DONG
    BCF T0CON,6	 ;SU DUNG BO DEM 16-BIT
    BCF T0CON,5	;SU DUNG NGUON DAO DONG BEN NGOAI (4MHz)
    BCF T0CON,3	;CHO PHEP GAN TY LE CHIA CHO TIMER
    BSF T0CON,2	;CHON GIA TRI Prescale value (TY LE CHIA)LA 1:32
    BCF T0CON,1
    BCF T0CON,0
    SETF START	;KHOI TAO GIA TRI BAN DAU CHO CAC BIEN DIEU KHIEN
    SETF ENABLE
    CLRF ENABLE1
    MOVLW D'50'
    MOVWF DEMLAN1
    
LOOP	;VONG LAP CHO SU KIEN NGAT
    MOVLW 0XFF
    CPFSEQ START
    GOTO LOOP

LOOP1	;BAT/TAT RELAY
    MOVLW 0X00
    CPFSEQ ENABLE1	;KIEM TRA NEU NHAN STOP THI DUNG DEM VA BAT DEN
    GOTO LOOP2
    MOVLW D'0'
    CPFSEQ DEMLAN1
    GOTO DEMTHU1
    BCF PORTD,3

LOOP2	;HIEN THI LED 7 DOAN VA DEN CANH BAO
    CALL HIENTHI
    CALL SANGDEN
    GOTO LOOP1

DEMTHU1	;GIAM "DEMLAN1" VA BAT DEN (TAO TRE CHO RELAY)
    DECF DEMLAN1,1
    GOTO BATDEN

BATDEN	;BAT DEN(RELAY)
    BSF PORTD,3
    GOTO LOOP2

HIENTHI	;HIEN THI LED 7 DOAN
    MOVLW 0XFF
    CPFSEQ START    ;KIEM TRA NEU NHAN OFF THI GOI CT CON "START1"
    GOTO START1
    MOVF DONVI,0
    CALL MANGMA	;LAY MA LED 7 DOAN TUONG UNG "DONVI"
    MOVWF DEM1	;SAO CHEP MA LED 7 DOAN VAO "DEM1"
    MOVF CHUC,0	;LAY MA LED 7 DOAN TUONG UNG "CHUC"
    CALL MANGMA
    MOVWF DEM2	;SAO CHEP MA LED 7 DOAN VAO "DEM1"
    MOVLW 0X01
    MOVWF PORTA	;CHO RA1 LEN MUC 1
    MOVF DEM1,0	;SAO CHEP MA LED 7 DOAN TU "DEM1" VAO WREG
    MOVWF PORTC	;XUAT MA LED 7 DOAN THEO "DEM1" RA PORTC
    CALL DELAY
    MOVLW 0X02
    MOVWF PORTA	;CHO RA2 LEN MUC 1
    MOVF DEM2,0	;SAO CHEP MA LED 7 DOAN TU "DEM2" VAO WREG
    MOVWF PORTC	;XUAT MA LED 7 DOAN THEO "DEM2" RA PORTC
    CALL DELAY
    RETURN

MANGMA
    ADDWF PCL,1
    DT 0xC0, 0xF9, 0xA4, 0xB0, 0x99, 0x92, 0x82, 0xF8, 0x80, 0x90

START1	;RESET CAC BIEN DEM, TAT DEN
    CLRF CHUC
    CLRF DONVI
    CLRF PORTA
    BCF PORTD,0
    BCF PORTD,1
    BCF PORTD,2
    RETURN

DELAY	;TAO TRE HIEN THI LED 7 DOAN
    MOVLW 0XF9
    MOVWF TMR0H
    MOVLW 0XE5
    MOVWF TMR0L
    BTFSS INTCON,2
    GOTO $-2
    BCF INTCON,2
    RETURN
;;;;;;;;;;;;CHUONG TRINH NGAT;;;;;;;;;;;;;;;;;
NGAT
    BTFSC INTCON,1
    GOTO NGAT_RB0   ;GOI CT CON KHI CO NGAT O CHAN RB0 (CAMBIEN)
    BTFSC INTCON3,0
    GOTO NGAT_RB1   ;GOI CT CON KHI CO NGAT O CHAN RB1	(ON/OFF)
    BTFSC INTCON3,1
    GOTO NGAT_RB2   ;GOI CT CON KHI CO NGAT O CHAN RB2	STOP/RESUME
    RETFIE

NGAT_RB1  ;NGAT INT1
    COMF START
    MOVLW 0XFF
    CPFSEQ START    ;NEU NHAN OFF THI GOI CT CON "RB1_OFF"
    GOTO RB1_OFF
    BCF INTCON3,0
    RETFIE

RB1_OFF     
    BCF PORTD,3
    SETF ENABLE
    CLRF ENABLE1
    BCF INTCON3,0
    RETFIE

NGAT_RB2 ; NGAT INT2
    COMF ENABLE
    COMF ENABLE1
    BSF PORTD,3
    BCF INTCON3,1
    RETFIE

NGAT_RB0	;NGAT INT0
    MOVWF W_TEMP
    MOVFF STATUS,STATUS_TEMP
    MOVLW 0XFF
    CPFSEQ START                     
    GOTO THOAT1                       
    CPFSEQ ENABLE
    GOTO THOAT
    MOVLW D'18'
    CPFSEQ CHUC
    GOTO CAP_NHAT
    CPFSEQ DONVI
    GOTO CAP_NHAT
    GOTO THOAT

CAP_NHAT    ;TANG "DONVI" "CHUC" DE DEM LEN
    INCF DONVI
    INCF DONVI
    MOVF DONVI,0
    XORLW D'20'	;SO SANH "DONVI" VOI 10, NEU = THI CHO VE 0 VA TANG "CHUC" LEN 1
    BTFSS STATUS,Z
    GOTO THOAT
    CLRF DONVI
    INCF CHUC
    INCF CHUC
    MOVF CHUC,0
    XORLW D'20'
    BTFSS STATUS,Z
    GOTO THOAT

THOAT
    CALL SANGDEN
    BSF PORTD,3
    MOVLW D'50'
    MOVWF DEMLAN1
THOAT1	;KHOI PHUC THANH GHI WREG VA STATUS REG
    MOVF W_TEMP,0
    MOVFF STATUS_TEMP,STATUS
    BCF INTCON,1
    RETFIE

SANGDEN	;SANG DEN CANH BAO
    BCF STATUS,C
    MOVLW D'2'
    SUBWF CHUC,0
    BTFSS STATUS,C
    GOTO SANG0	;0<CHUC<10
    BCF STATUS,C
    MOVLW D'4'
    SUBWF CHUC,0
    BTFSS STATUS,C
    GOTO SANG1	;10<CHUC<20
    BCF STATUS,C
    MOVLW D'6'
    SUBWF CHUC,0
    BTFSS STATUS,C
    GOTO SANG2	;30<CHUC<40
    BCF STATUS,C
    MOVLW D'8'
    SUBWF CHUC,0
    BTFSS STATUS,C
    GOTO SANG3	;40<CHUC<99
    RETURN

SANG0
    BCF PORTD,0
    BCF PORTD,1
    BCF PORTD,2
    RETURN

SANG1
    BSF PORTD,0
    BCF PORTD,1
    BCF PORTD,2
    RETURN

SANG2
    BCF PORTD,0
    BSF PORTD,1
    BCF PORTD,2
    RETURN

SANG3
    BCF PORTD,0
    BCF PORTD,1
    BSF PORTD,2
    RETURN

    END


